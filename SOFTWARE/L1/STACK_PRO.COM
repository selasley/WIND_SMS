$!
$! Procedure:  STACK_PRO.COM
$! Author:     Chris Cohen
$! Purpose:    Generate a stack plot from one or more WIND L1 files.
$!
$! Parameters:
$!
$! P1 - The number of L1 files to be processed.  If omitted the user will be
$!      prompted for its value.
$! P2 - If given, the file of a file containing, one per line, the names
$!      of the L1 files to be processed.  This file is assumed to be in a
$!      format generated by the DCL DIRECTORY command.
$!      If omitted the user will be prompted for each L1 filename.
$!      In both cases only the file is to be given; it is assummed to exist in
$!      the directory WIND_L1_DATA:.
$!
$! STACK_PRO.COM generates a command procedure named ST.COM which will run the
$! program STACK, which resides in the directory SMS1:[COHEN.WIND.SWICS], on
$! each L1 file.  The output of this program will then be fed to an IDL
$! procedure named STACK.PRO, which resides in the directory
$! SMS1:[WIND.SOFTWARE.L1].  STACK.PRO will generate a stack plot and write it
$! to a file in PostScript format.  Finally, this file, named IDL.PS, will be
$! sent to the default printer. 
$! 
$! After all the commands to perform the above operations on all the given L1
$! files have been written to ST.COM, ST.COM is submitted as a batch job.
$!
$i = 1
$open/write st st.com
$write st "$ set default scratch:"
$if p1 .eqs. "" then inquire p1 "Enter number of files to run"
$if p2 .nes. "" 
$then 
$open/read list 'p2'                    !read headers
$read list filename
$read list filename
$read list filename
$endif
$loop:
$if p2 .eqs. "" 
$then
$inquire filename "Enter filename"
$else
$read list filename
$if f$extract(0,3,filename) .eqs. "" then goto end
$endif
$dot = f$locate(".",filename) 
$outfile = f$extract(0,dot,filename) 
$show symbol outfile
$write st "$run sms1:[cohen.wind.swics]stack"
$write st "smsdisk4:[wind.l2data.swics.routine]", outfile
$write st "1"
$write st "1"
$write st "wind_l1_data:", filename"
$write st "y"
$write st "n"
$write st "n"
$write st "$idl"
$write st "set_plot, 'ps'"
$write st "device, /portrait, /inches, xsize=7., ysize=9., xoff=1., yoff=1"
$write st ".run sms1:[wind.software.l1]stack"
$write st "smsdisk4:[wind.l2data.swics.routine]",outfile,".fsr"
$write st "exit"
$write st "$psprint idl.ps"
$i = i + 1
$if i .le. p1 then goto loop
$!
$end:
$write st "$exit"
$close st
$if p2 .nes. "" then close list 
$submit/noprint/notify st
$exit 1
