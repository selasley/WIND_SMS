$!
$! Procedure:  PROCESS.COM
$! Author:     Chris Cohen
$! Purpose:    Generate a plot from one or more WIND L1 files.
$!
$! This file runs WVELOCITY and HK_SWICS:
$! 1.  WVELOCITY - located in sms1:[cohen.wind.swics]
$!     This program generates a data file which contains proton and alpha 
$!     velocities, kinetic temperatures, oxygen ionziation temperature, CNO/Fe
$!     abundance ratio, velocity difference between protons and alphas, and 
$!     kinetic temperature ratio of alphas to protons all as a function of time.
$!     output files are named *.outv
$! 2.  HK_SWICS - located in sms1:[cohen.wind.swics]
$!     This program generates a data file which contains the status of some
$!     of the housekeeping parameters specific to SWICS.  Status is only printed
$!     if it involves a change.
$!     output files are named *.outhk
$! The output of both *.outv and *.fsr are written in the directory
$!     smsdisk4:[wind.l2data.swics.routine]
$! '*' corresponds to the filename used for the L1 file, 
$!   e.g., WI_L1_SMS_19960421_V01
$!
$! The output of WVELOCITY is fed to TPLOT.PRO which generates plot
$!   (one per day/L1 file in one-hour resolution) that contains
$!   proton and alpha velocities and kinetic temperatures; oxygen ionization
$!   temperature and CNO/Fe abundance ratio.
$!
$! Parameters:
$!
$! P1 - The number of L1 files to be processed.  If omitted the user will be
$!      prompted for its value.
$! P2 - If given, the file of a file containing, one per line, the names
$!      of the L1 files to be processed.  This file is assumed to be in a
$!      format generated by the DCL DIRECTORY command.
$!      If omitted the user will be prompted for each L1 filename.
$!      In both cases only the file is to be given; it is assummed to exist in
$!      the directory WIND_L1_DATA:.
$!
$! PROCESS.COM generates a command procedure named PRO.COM which will run the
$! program WVELOCITY, which resides in the directory SMS1:[COHEN.WIND.SWICS],
$! on each L1 file.  The output of this program will then be fed to an IDL
$! procedure named TPLOT.PRO, which resides in the directory
$! SMS1:[WIND.SOFTWARE.SWICS]. TPLOT.PRO will generate a plot and write it
$! to a file in PostScript format.  This file, named IDL.PS, will then be
$! sent to the default printer.  Finally the program HK_SWICS, located in
$! SMS1:[COHEN.WIND.SWICS], will be run, and its output file will be
$! printed.
$! 
$! After all the commands to perform the above operations on all the given L1
$! files have been written to PRO.COM, PRO.COM is submitted as a batch job.
$!
$! set sms1:[wind.software.swics.scratch] as the working directory
$!
$i = 1
$open/write pro pro.com
$write pro "$set default sms1:[wind.software.swics.scratch]"
$if p1 .eqs. "" then inquire p1 "Enter number of files to run"
$if p2 .nes. "" 
$then 
$open/read list 'p2                    !read headers
$read list filename
$read list filename
$read list filename
$endif
$loop:
$if p2 .eqs. "" 
$then
$inquire filename "Enter filename"
$else
$read list filename
$if f$extract(0,3,filename) .eqs. "" then goto end
$endif
$dot = f$locate(".",filename) + 1
$outfile = f$extract(0,dot,filename) + "out"
$show symbol outfile
$write pro "$run sms1:[cohen.wind.swics]wvelocity"
$write pro "smsdisk4:[wind.l2data.swics.routine]", outfile, "v"
$write pro "wind_l1_data:", filename"
$write pro "y"
$write pro "n"
$write pro "n"
$write pro "$!                         idl"
$write pro "$idl"
$write pro ".run sms1:[wind.software.swics]tplot"
$write pro "2"
$write pro "1"
$write pro "n"
$write pro "smsdisk4:[wind.l2data.swics.routine]", outfile, "v"
$write pro "exit"
$write pro "$psprint idl.ps"
$write pro "$!                         hk"
$write pro "$run sms1:[cohen.wind.swics]hk_swics"
$write pro "wind_l1_data:",filename
$write pro "y"
$write pro "n"
$write pro "smsdisk4:[wind.l2data.swics.routine]", outfile, "hk"
$write pro "n"
$write pro "$print smsdisk4:[wind.l2data.swics.routine]", outfile, "hk"
$write pro "$!"
$i = i + 1
$if i .le. p1 then goto loop
$!
$end:
$close pro
$if p2 .nes. "" then close list 
$submit/noprint/notify pro
$exit 1
