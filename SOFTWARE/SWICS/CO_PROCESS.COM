$!
$! Procedure:  CO_PROCESS.COM
$! Author:     Chris Cohen
$! Purpose:    Generate a plot for WIND webpage from one or more WIND L1 files.
$!
$! This file runs the program CO_OUTPUT located in sms1:[cohen.wind.swics]
$!    The program CO_OUTPUT generates a data file which contains alpha velocity,
$!    oxygen and carbon ionization temperatures and C/O abundance ratio.
$!    Output files are named *.cosw written in the directory
$!    smsdisk4:[wind.l2data.swics.routine] 
$! '*' corresponds to the filename used for the L1 file, 
$!     e.g., WI_L1_SMS_19960421_V01
$!
$! The idl program COPLOT.PRO is then used to generate a *.GIF file (one
$! per day/L1 file) which is placed in the directory SMS1:[WIND.WWW.SWICS] for 
$! access by the Wind homepage.
$!
$! Parameters:
$!
$! P1 - The number of L1 files to be processed.  If omitted the user will be
$!      prompted for its value.
$! P2 - If given, the file of a file containing, one per line, the names
$!      of the L1 files to be processed.  This file is assumed to be in a
$!      format generated by the DCL DIRECTORY command.
$!      If omitted the user will be prompted for each L1 filename.
$!      In both cases only the file is to be given; it is assummed to exist in
$!      the directory WIND_L1_DATA:.
$!
$! CO_PROCESS.COM generates a command procedure named CO_PRO.COM which will run
$! the program CO_OUTPUT, which resides in the directory
$! SMS1:[COHEN.WIND.SWICS], on each L1 file.  The output of this program will
$! then be fed to an IDL procedure named COPLOT.PRO, which resides in the
$! directory SMS1:[WIND.SOFTWARE.SWICS].  COPLOT.PRO will generate a *.GIF file.
$! 
$! After all the commands to perform the above operations on all the given L1
$! files have been written to CO_PRO.COM, CO_PRO.COM is submitted as a batch
$! job. 
$!
$! set sms1:[wind.software.swics.scratch] as the working directory
$!
$i = 1
$open/write pro co_pro.com
$write pro "$set default sms1:[wind.software.swics.scratch]"
$if p1 .eqs. "" then inquire p1 "Enter number of files to run"
$if p2 .nes. "" 
$then 
$open/read list 'p2                    !read headers
$read list filename
$read list filename
$read list filename
$endif
$loop:
$if p2 .eqs. "" 
$then
$inquire filename "Enter filename"
$else
$read list filename
$if f$extract(0,3,filename) .eqs. "" then goto end
$endif
$dot = f$locate(".",filename) + 1
$year = f$locate("199",filename) + 2
$coutfile = f$extract(year,dot-year,filename) + "gif"
$outfile = f$extract(0,dot,filename) + "cosw"
$write pro "$run sms1:[cohen.wind.swics]co_output"
$write pro "smsdisk4:[wind.l2data.swics.routine]", outfile
$write pro "wind_l1_data:", filename"
$write pro "y"
$write pro "n"
$write pro "240"
$write pro "n"
$write pro "n"
$write pro "$!                         idl"
$write pro "$idl"
$write pro ".run sms1:[wind.software.swics]coplot"
$write pro "2"
$write pro "smsdisk4:[wind.l2data.swics.routine]", outfile
$write pro "sms1:[wind.www.swics]",coutfile
$write pro "exit"
$write pro "$!"
$i = i + 1
$if i .le. p1 then goto loop
$!
$end:
$close pro
$if p2 .nes. "" then close list 
$submit/noprint/notify co_pro
$exit 1
